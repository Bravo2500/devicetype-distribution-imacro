VERSION BUILD=9030808 RECORDER=FX

TAB T=1
SET !DATASOURCE keywords.csv
SET !DATASOURCE_COLUMNS 1
SET !DATASOURCE_LINE {{!loop}}

SET !TIMEOUT_STEP 10
SET !VAR8 EVAL("var randomNumber=Math.floor(Math.random()+0.2); randomNumber;")
SET !VAR9 EVAL("var randomNumber=Math.floor(Math.random()+7); randomNumber;")

'Keyword aus DataSource pasten und Suchvolumen-abrufen-Button klicken
TAG POS=3 TYPE=TEXTAREA ATTR=ID:gwt-debug-upload-text-box CONTENT={{!COL1}}
WAIT SECONDS={{!VAR8}}
TAG POS=20 TYPE=DIV ATTR=TXT:Suchvolumenabrufen
WAIT SECONDS={{!VAR9}}


'Keyword und aggregiertes durchschnittliches Suchvolumen auslesen
SET !VAR0 {{!COL1}}
TAG POS=1 TYPE=DIV ATTR=ID:gwt-debug-column-SEARCH_VOLUME_PRIMARY-row-0-0 EXTRACT=TXT
SET trimmedSearchVolume EVAL("\"{{!EXTRACT}}\".replace(/^\\s*|\\s*$/g, \"\");")
ADD !VAR0 ;{{trimmedSearchVolume}}
SET !EXTRACT NULL


'Ersten Datensatz abfragen Var1
TAG POS=1 TYPE=TD ATTR=CLASS:aplos-legend-label-cell EXTRACT=TXT
SET !VAR1 {{!EXTRACT}}
SET !EXTRACT NULL
TAG POS=1 TYPE=TD ATTR=CLASS:aplos-legend-value-cell EXTRACT=TXT
SET Var1Separated EVAL("\"{{!EXTRACT}}\".replace(/ \\(/g, ';');")
SET Var1Processed EVAL("\"{{Var1Separated}}\".replace(/\\)/g, '');")
ADD !VAR1 ;{{Var1Processed}}
SET !EXTRACT NULL

'Zweiten Datensatz abfragen Var2
TAG POS=2 TYPE=TD ATTR=CLASS:aplos-legend-label-cell EXTRACT=TXT
SET !VAR2 {{!EXTRACT}}
SET !EXTRACT NULL
TAG POS=2 TYPE=TD ATTR=CLASS:aplos-legend-value-cell EXTRACT=TXT
SET Var2Separated EVAL("\"{{!EXTRACT}}\".replace(/ \\(/g, ';');")
SET Var2Processed EVAL("\"{{Var2Separated}}\".replace(/\\)/g, '');")
ADD !VAR2 ;{{Var2Processed}}
SET !EXTRACT NULL

'Dritten Datensatz abfragen Var3
TAG POS=3 TYPE=TD ATTR=CLASS:aplos-legend-label-cell EXTRACT=TXT
SET !VAR3 {{!EXTRACT}}
SET !EXTRACT NULL
TAG POS=3 TYPE=TD ATTR=CLASS:aplos-legend-value-cell EXTRACT=TXT
SET Var3Separated EVAL("\"{{!EXTRACT}}\".replace(/ \\(/g, ';');")
SET Var3Processed EVAL("\"{{Var3Separated}}\".replace(/\\)/g, '');")
ADD !VAR3 ;{{Var3Processed}}
SET !EXTRACT NULL

'Datensätze Sortieren nach Computer, Tablet, Mobilgeräte
SET valuesComputer EVAL("if(\"{{!VAR1}}\".charAt(0) == 'C') \"{{!VAR1}}\"; else if(\"{{!VAR2}}\".charAt(0) == 'C') \"{{!VAR2}}\"; else \"{{!VAR3}}\";")
SET valuesTablet EVAL("if(\"{{!VAR1}}\".charAt(0) == 'T') \"{{!VAR1}}\"; else if(\"{{!VAR2}}\".charAt(0) == 'T') \"{{!VAR2}}\"; else \"{{!VAR3}}\";")
SET valuesMobile EVAL("if(\"{{!VAR1}}\".charAt(0) == 'M') \"{{!VAR1}}\"; else if(\"{{!VAR2}}\".charAt(0) == 'M') \"{{!VAR2}}\"; else \"{{!VAR3}}\";")


'Inhalte der Variablen mit einem Semikolon getrennt zusammenführen und in CSV speichern
SET !EXTRACT {{!VAR0}};{{valuesComputer}};{{valuesTablet}};{{valuesMobile}}
SAVEAS TYPE=EXTRACT FOLDER=* FILE=tutorial_file_001.csv
